
# Specify a server (science program database)
# ATC		(ATC Development)
# HILO		(Hilo Development)
# HOME		(Home Development)
# DEPLOYED        (Deployed Server)
set SERVER = ATC

set TELESCOPE = "" 
set INTERNAL_FRAMES = -internalframes 
set OMP_FLAG = "" 
set USAGE = "Usage: $0 [-h] [-[no]internalframes] [-[no]omp] [-server ATC|HILO|HOME|DEPLOYED] <telescope>\n  where <telescope> is one of: ${TELESCOPE_LIST}\n"
 
############################## 
#                            # 
# Parsing arguments          # 
#                            # 
# ############################ 
while ("$1" != "")
  if("$1" == "-h") then 
    printf "${USAGE}" 
    exit 
  else if("$1" == "-internalframes") then 
    set INTERNAL_FRAMES = -internalframes 
  else if("$1" == "-nointernalframes") then 
    set INTERNAL_FRAMES = -nointernalframes 
  else if("$1" == "-omp") then 
    set OMP_FLAG = -DOMP 
  else if("$1" == "-noomp") then 
    set OMP_FLAG = "" 
  else if("$1" == "-server") then
    shift
    set SERVER = $1
  else if(-d ${CFG_DIRS}/"$1") then 
    set TELESCOPE = $1 
  else 
    echo Telescope configuration directory $1 not found in ${CFG_DIRS} 
    printf "${USAGE}" 
    exit 
  endif 
  shift
end 
 
if("${TELESCOPE}" == "") then 
  printf "${USAGE}" 
  exit 
endif 

echo Settings:

echo "  Telescope:       ${TELESCOPE}"
echo "  Server:          ${SERVER}"

if("${OMP_FLAG}" == "-DOMP") then
  echo "  Database:        OMP/XML"
else if ("${OMP_FLAG}" == "") then
  echo "  Database:        RMI/SGML"
else
  printf "\nError: Variable OMP_FLAG has invalid value \042${OMP_FLAG}\042. Allowed values: \042-DOMP\042, \042\042.\n\n"
endif

if("${INTERNAL_FRAMES}" == "-internalframes") then
  echo "  Internal Frames: Yes"
else if("${INTERNAL_FRAMES}" == "-nointernalframes") then
  echo "  Internal Frames: No"
else
  printf "\nError: Variable INTERNAL_FRAMES has invalid value \042${INTERNAL_FRAMES}\042. Allowed values: \042-internalframes\042, \042-nointernalframes\042.\n\n"
endif

echo "Use ot -h for information on how to change these settings."


set CFG_TELESCOPE_DIR = ${CFG_DIRS}/${TELESCOPE} 
 
${JAVA} -ms5m -mx100m -oss5m ${OMP_FLAG} -DSERVER=${SERVER} -Djsky.catalog.skycat.config=${CFG_TELESCOPE_DIR}/skycat.cfg -Dot.cfgdir=${CFG_TELESCOPE_DIR}/ -Dot.resource.cfgdir=${TELESCOPE}/ -Dot.userdir=${OT_USERDIR} -classpath ${CLASSPATH}:${CFG_DIRS} jsky.app.ot.OT ${INTERNAL_FRAMES}

