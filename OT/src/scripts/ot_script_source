set CFG_DIRS = `pwd`/../cfg/ot/

set OT_USERDIR=`pwd`

set JAVA = `which java`
 
# The telescope names in this list should correspond to the directories in CFG_DIRS. 
# (generated from ../conf/make.conf) 
set TELESCOPE_LIST = "ukirt jcmt"  

set TELESCOPE = "" 
set INTERNAL_FRAMES = -nointernalframes 
set EDFREQ_CFG = "edfreq/acsisCfg.xml"
set DISALLOW_MULTI_BW_PROP = ""
set USAGE = "Usage: $0 [-h] [-[no]internalframes] <telescope>\n  where <telescope> is one of: ${TELESCOPE_LIST}\n"
set XMLFILES = ""
set OT_CFG_FILE = ""
 
############################## 
#                            # 
# Parsing arguments          # 
#                            # 
# ############################ 
while ("$1" != "")
  if("$1" == "-h") then 
    printf "${USAGE}" 
    exit 
  else if("$1" == "-internalframes") then 
    set INTERNAL_FRAMES = -internalframes 
  else if("$1" == "-nointernalframes") then 
    set INTERNAL_FRAMES = -nointernalframes 
  else if( `echo $1 | grep -i ".xml"` != "" ) then
    set XMLFILES = "$XMLFILES $1"
  else if(-d ${CFG_DIRS}/"$1") then 
    set TELESCOPE = $1 
  else 
    echo Telescope configuration directory $1 not found in ${CFG_DIRS} 
    printf "${USAGE}" 
    exit 
  endif 
  shift
end 
 
if("${TELESCOPE}" == "") then 
  printf "${USAGE}" 
  exit 
endif 

if("${EDFREQ_CFG}" == "edfreq/acsisCfg.xml") then
  set DISALLOW_MULTI_BW_PROP = "-DDISALLOW_MULTI_BW"
endif

echo Settings:

echo "  Telescope:       ${TELESCOPE}"

if("${INTERNAL_FRAMES}" == "-internalframes") then
  echo "  Internal Frames: Yes"
else if("${INTERNAL_FRAMES}" == "-nointernalframes") then
  echo "  Internal Frames: No"
else
  printf "\nError: Variable INTERNAL_FRAMES has invalid value \042${INTERNAL_FRAMES}\042. Allowed values: \042-internalframes\042, \042-nointernalframes\042.\n\n"
endif

echo "Use ot -h for information on how to change these settings."

if (${?OT_CATALOG_FILE}) then
    set CATALOG_FILE = ${OT_CATALOG_FILE}
else
    set CATALOG_FILE
endif

set CFG_TELESCOPE_DIR = ${CFG_DIRS}/${TELESCOPE} 

${JAVA} -Xms10m -Xmx500m -DTELESCOPE=${TELESCOPE} -Djsky.catalog.skycat.config=${CFG_TELESCOPE_DIR}/skycat.cfg -Dot.cfgdir=${CFG_TELESCOPE_DIR}/ -Dot.resource.cfgdir=./ -Dot.userdir=${OT_USERDIR} -DOT_CATALOG_FILE=${CATALOG_FILE} ${OT_CFG_FILE} -classpath ${CLASSPATH}:${CFG_TELESCOPE_DIR} -Dapple.laf.useScreenMenuBar=true jsky.app.ot.OT ${INTERNAL_FRAMES} ${XMLFILES}
