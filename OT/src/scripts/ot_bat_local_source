echo off
rem /*==============================================================*/
rem /*                                                              */
rem /*                UK Astronomy Technology Centre                */
rem /*                 Royal Observatory, Edinburgh                 */
rem /*                 Joint Astronomy Centre, Hilo                 */
rem /*                   Copyright (c) PPARC 2001                   */
rem /*                                                              */
rem /*==============================================================*/
rem $Id$

rem This file could be overwritten by gmake in which case modifications would be lost.
rem For editing this script and keeping the modifications make a copy of this script in
rem this directory and edit the copy.

rem For the default CLASSPATH settings to work the script has to be in its usual location in an
rem CVS orac3 checkout or export directory tree (e.g. SOME_CVS_CHECKOUT_DIR/orac3/OT/install/bin)
rem and it has to be started from within its own directory or by double click.
set CLASSPATH=..\..\..\OMP\tools\activation.jar;..\..\..\OMP\tools\mail.jar;..\..\..\OMP\tools\soap.jar;..\..\..\ORAC\tools\jh.jar;..\..\..\ORAC\tools\jhall.jar;..\..\..\ORAC\tools\jhbasic.jar;..\..\..\ORAC\tools\jhtools.jar;..\..\..\ORAC\tools\xercesImpl.jar;..\..\..\ORAC\tools\xmlParserAPIs.jar;..\..\tools\diva.jar;..\..\tools\dom4j.jar;..\..\tools\fits.jar;..\..\tools\graph.jar;..\..\tools\hcompress.jar;..\..\tools\image.jar;..\..\tools\jacl.jar;..\..\tools\jai_codec.jar;..\..\tools\jai_core.jar;..\..\tools\jcommon.jar;..\..\tools\jdom-b4.jar;..\..\tools\jel.jar;..\..\tools\jfreechart.jar;..\..\tools\jsky-catalog-gui.jar;..\..\tools\jsky-catalog.jar;..\..\tools\jsky-coords.jar;..\..\tools\jsky-elevation-plot.jar;..\..\tools\jsky-html.jar;..\..\tools\jsky-image-gui.jar;..\..\tools\jsky-image.jar;..\..\tools\jsky-science.jar;..\..\tools\jsky-timeline.jar;..\..\tools\jsky-util-gui.jar;..\..\tools\jsky-util.jar;..\..\tools\jsky.jar;..\..\tools\jskycat.jar;..\..\tools\junit.jar;..\..\tools\log4j-1.2.14.jar;..\..\tools\mlibwrapper_jai.jar;..\..\tools\tcljava.jar;..\..\tools\util.jar;..\..\tools\pal.jar;..\..\..\EDFREQ\tools\JSX1.0.1.1.jar

rem The directory containing the telescope specific configuration directories (CFG_DIRS\ukirt, CFG_DIRS\jcmt)
set CFG_DIRS=..\cfg

rem If there is no appropriate jdk1.3 java in your PATH (with JAI 1.0.2 installed)
rem then set one here.
set JAVA=java 

rem Allowed values: ATC, HILO, HOME, DEPLOYED
set SERVER=ATC 

rem INTERNAL_FRAMES=-internalframes      (One big OT frame with internal frames for Science Programmes) 
rem INTERNAL_FRAMES=-nointernalframes    (Individual frame for each Science Programme)
rem Variable is reset if one of the following command line options is
rem used: -internalframes, -nointernalframes.
set INTERNAL_FRAMES=-nointernalframes

set FREQ_EDITOR_CFG_RESOURCE=edfreq\dasCfg.xml
set DISALLOW_MULTI_BW_PROP=

rem Allowed values: ukirt, jcmt
rem Variable is reset if a command line argument is specified (e.g. ukirt or jcmt)
set TELESCOPE=ukirt

:LOOP
IF (%1)==(-h)                 GOTO USAGE
IF (%1)==(-internalframes)    set INTERNAL_FRAMES=-internalframes
IF (%1)==(-nointernalframes)  set INTERNAL_FRAMES=-nointernalframes
IF (%1)==(-nodas)             set FREQ_EDITOR_CFG_RESOURCE=edfreq\acsisCfg.xml
IF (%1)==(-das)               set FREQ_EDITOR_CFG_RESOURCE=edfreq\dasCfg.xml
IF (%1)==(-server)            set SERVER=%2
IF (%1)==(-server)            SHIFT
IF (%1)==(ukirt)              set TELESCOPE=ukirt
IF (%1)==(jcmt)               set TELESCOPE=jcmt
SHIFT
IF NOT (%1)==() GOTO LOOP

IF NOT EXIST %CFG_DIRS%\%TELESCOPE% echo Telescope configuration directory %TELESCOPE% not found in %CFG_DIRS%
IF NOT EXIST %CFG_DIRS%\%TELESCOPE% GOTO USAGE

echo Settings:
                                            echo   Telescope:       %TELESCOPE%
rem					    echo   Server:          %SERVER%
IF (%INTERNAL_FRAMES%)==(-internalframes)   echo   Internal Frames: Yes
IF (%INTERNAL_FRAMES%)==(-nointernalframes) echo   Internal Frames: No
echo "Use ot -h for information on how to change these settings."

set CFG_TELESCOPE_DIR=%CFG_DIRS%\%TELESCOPE%

IF (%FREQ_EDITOR_CFG_RESOURCE%)==(edfreq\acsisCfg.xml) set DISALLOW_MULTI_BW_PROP=-DDISALLOW_MULTI_BW

echo on
%JAVA% -Xms5m -Xmx500m -DTELESCOPE=%TELESCOPE% -DSERVER=%SERVER% -Djsky.catalog.skycat.config=%CFG_TELESCOPE_DIR%\skycat.cfg -Dot.cfgdir=%CFG_TELESCOPE_DIR%\ -Dot.resource.cfgdir=.\ -classpath %CLASSPATH%;%CFG_TELESCOPE_DIR% jsky.app.ot.OT %INTERNAL_FRAMES%
echo off

GOTO END

:USAGE
echo Usage: java ot [-h] [-[no]internalframes] ukirt jcmt

:END
echo OT finished.
pause
echo on

